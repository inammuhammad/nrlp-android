buildscript {
    repositories {
        maven {
            url '../../../../lib'       // For the DexGuard plugin.
        }
        google()                        // For the Android plugin.
        mavenCentral()                       // For anything else.
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:7.0.0'
        //noinspection GradleDynamicVersion
        classpath 'com.guardsquare:dexguard-gradle-plugin:+'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'dexguard'

repositories {
    flatDir { dirs 'libs' } // For the DexGuard runtime library.
    google()  // For the Android support libraries.
    mavenCentral() // For any other libraries.
}

dependencies {
   // Only needed when using a custom RASP callback
   compileOnly(name: 'dexguard-rasp', ext: 'jar')
}

android {
    compileSdkVersion 30

    useLibrary 'org.apache.http.legacy'

    sourceSets {
        main {
            manifest.srcFile 'AndroidManifest.xml'
            java.srcDirs         = ['src']
            resources.srcDirs    = ['src']
            aidl.srcDirs         = ['src']
            renderscript.srcDirs = ['src']
            res.srcDirs          = ['res']
        }
    }

    defaultConfig {
        minSdkVersion    11
        targetSdkVersion 30
    }

    buildTypes {
        release {
            minifyEnabled false
            shrinkResources false
        }
    }
}

android.libraryVariants.all { variant ->
    task "copyAar${variant.name.capitalize()}"(type: Copy) {
        from 'build/outputs/aar'
        include "library-${variant.name}-protected.aar"
        into '../app/libs'

        dependsOn variant.assembleProvider
        tasks.getByName("assemble${variant.name.capitalize()}").finalizedBy(it)
    }
}

dexguard {
    path = '../../../..'
    // We can build the sample with the sample license file
    license = '../../../../lib/dexguard-license.txt'
    configurations {
        debug {
            defaultConfiguration 'dexguard-library-debug.pro'
            defaultConfiguration 'dexguard-rasp.pro'
            configuration 'dexguard-project.txt'
            configuration 'proguard-project.txt'
        }
        release {
            defaultConfiguration 'dexguard-library-release-aggressive.pro'
            defaultConfiguration 'dexguard-rasp.pro'
            configuration 'dexguard-project.txt'
            configuration 'proguard-project.txt'
        }
    }
}
